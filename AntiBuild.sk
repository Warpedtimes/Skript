# Anti Building Skript
# Skript-ver: 2.6-beta2
# Tested mc-ver: 1.17
# Author: Warpedtimes (https://github.com/Warpedtimes/)

# False = Building allowed
# True = Building not allowed
# Permission: "build.allow" Players with this permission will recieve a error message telling them to enable building.
#			   Players without will only recieve the error.
# Will automatically set building to disabled on join.

options:
	errorPrefix: &cERROR &8|
	antiBuildPrefix: &cAnti-Build &8|

on break:
	if {antiBuild.%player's uuid%} is true:
		cancel event
		if player has permission "build.allow":
			message "{@errorPrefix} &7You cannot build here. Use &c/build&7 to enable building mode."
		else:
			message "{@errorPrefix} &7You cannot build here."

on place:
	if {antiBuild.%player's uuid%} is true:
		cancel event
		if player has permission "build.allow":
			message "{@errorPrefix} &7You cannot build here. Use &c/build&7 to enable building mode."
		else:
			message "{@errorPrefix} &7You cannot build here."

command build:
	permission: build.allow
	permission message: {@errorPrefix} You do not have permission to do this!
	trigger:
		if {antiBuild.%player's uuid%} is true:
			set {antiBuild.%player's uuid%} to false
			message "{@antiBuildPrefix} &7Building &2&lenabled. &7Use &c/build&7 to disable again."
		else:
			set {antiBuild.%player's uuid%} to true
			message "{@antiBuildPrefix} &7Building &c&ldisabled. &7Use &c/build&7 to enable again."

on join:
	if {antiBuild.%player's uuid%} is not set:
		set {antiBuild.%player's uuid%} to true
	if {antiBuild.%player's uuid%} is false:
		set {antiBuild.%player's uuid%} to true
		wait 3 seconds
		message "{@antiBuildPrefix} &7Building &c&ldisabled. &7Use &c/build&7 to enable again."
